buildscript {
    repositories {
        if (!project.hasProperty("forgeGradleBinFolder")) {
            maven {
                url = 'https://files.minecraftforge.net/maven'
            }
        }
        mavenCentral()
    }
    dependencies {
        if (!project.hasProperty("forgeGradleBinFolder")) {
            classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.+', changing: true
        } else {
            implementation files(project.getProperty("forgeGradleBinFolder").toString())
        }
    }
}

// Plugins
plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'java'
    id 'maven-publish'
}
apply plugin: 'net.minecraftforge.gradle'
import net.minecraftforge.gradle.common.task.SignJar

// apply plugin: 'maven-publish'

// Mod info --------------------------------------------------------------------

def mod_id = "endor-util"
group = "endorh.util"
version = "0.2.30"
def group_slashed = project.group.replaceAll("\\.", "/"),
    classname = "Endor Util",
    maven_artifact = "${group}:${mod_id}:${version}"

// Attributes
def display_name = "Endor Util",
    vendor = "Endor H",
    credits = "",
    authors = "Endor H",
    issue_tracker = "",
    page = "",
    update_json = "",
    logo_file = "${mod_id}.png",
    description = '''
Modding utilities I thought could be reused.
Still quite small, but feel free to use.
'''

// License
def license = "LGPL"

// Dependencies
def mc_version = "1.16.5",
    forge = "36.1.0",
    forge_version = "${mc_version}-${forge}"

// Jar attributes
archivesBaseName = "${mod_id}-${mc_version}"

def jar_attributes = [
    "Specification-Title"     : "${mod_id}",
    "Specification-Vendor"    : "${vendor}",
    "Specification-Version"   : "1",
    "Implementation-Title"    : project.name,
    "Implementation-Version"  : "${version}",
    "Implementation-Vendor"   : "${vendor}",
    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
    "Maven-Artifact"          : "${maven_artifact}"
]

def mod_properties = [
    modid: mod_id,
    display: display_name,
    version: project.version,
    mcversion: mc_version,
    vendor: vendor,
    authors: authors,
    credits: credits,
    license: license,
    page: page,
    issue_tracker: issue_tracker,
    update_json: update_json,
    logo_file: logo_file,
    description: description,
    group: group,
    class_name: classname,
    group_slashed: group_slashed
]

// Java options -------------------------------------------------------------------

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

// Unicode support
compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
javadoc.options.encoding = 'UTF-8'

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

java {
    withSourcesJar()
    // withJavadocJar()
}

// Minecraft options --------------------------------------------------------------

minecraft {
    // Mappings:
    // |_Channel__|_Version___|________________________________________
    // | snapshot | YYYYMMDD  | Built nightly
    // | stable   | #         | Built at the discretion of the MCP team
    // | official | MCVersion | From Mojang mapping files
    //
    // Official mappings channel license:
    // https://github.com/MinecraftForge/MCPConfig/blob/master/Mojang.md
    //mappings channel: 'official', version: '1.16.5'
    mappings channel: 'snapshot', version: '20201028-1.16.3'

    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Run configurations
    runs {
        client {
            //noinspection GroovyAssignabilityCheck
            workingDirectory project.file('run')

            // Allowed flags: SCAN, REGISTRIES, REGISTRYDUMP
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                simpleconfig {
                    //noinspection GroovyAssignabilityCheck
                    source sourceSets.main
                }
            }
        }

        server {
            //noinspection GroovyAssignabilityCheck
            workingDirectory project.file('run')

            // Allowed flags: SCAN, REGISTRIES, REGISTRYDUMP
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                simpleconfig {
                    //noinspection GroovyAssignabilityCheck
                    source sourceSets.main
                }
            }
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

// Project dependencies -----------------------------------------------------------

repositories {
    maven {
        name = "Cloth Config API"
        url = "https://maven.shedaniel.me/"
    }
    maven {
        url 'https://repo.maven.apache.org/maven2'
        name 'Maven Central'
    }
}

dependencies {
    // IDE
    implementation 'org.jetbrains:annotations:21.0.1'
    implementation 'org.junit.jupiter:junit-jupiter:5.7.2'

    // Minecraft
    minecraft "net.minecraftforge:forge:${forge_version}"

    // I shouldn't have learnt PCRE before other (useless) Regex implementations
    // Now I can't live without recursive patterns
    // https://github.com/florianingerl/com.florianingerl.util.regex
    implementation "com.github.florianingerl.util:regex:1.1.9"

    // Simple regex valid matches generator
    implementation "com.github.curious-odd-man:rgxgen:1.3"

}

// Tasks --------------------------------------------------------------------------

test {
    useJUnitPlatform()
}

classes.dependsOn extractNatives // Make sure the natives are extracted on compile

shadowJar {
    manifest {
        attributes(jar_attributes)
    }

    // Include only Cloth Config API in the shadow jar
    dependencies {
        //noinspection GroovyAssignabilityCheck
        include dependency("com.github.florianingerl.util:regex:1.1.9")
        include dependency("com.github.curious-odd-man:rgxgen:1.3")
    }

    // Relocate the root package
    relocate "com.florianingerl", "endorh.util.shadowed.com.florianingerl"
    relocate "com.github.curiousoddman", "endorh.util.shadowed.com.github.curiousoddman"

    classifier '' // Replace default jar

    // Preferred method to reobfuscate the jar file
    finalizedBy 'reobfJar'
}

reobf {
    shadowJar {}
}

// Jar attributes
jar {
    manifest {
        attributes(jar_attributes)
    }

    classifier 'clean'

    // Preferred method to reobfuscate the jar file
    finalizedBy 'reobfJar'
}

// task deobfJar(type: Jar, dependsOn: classes) {
//     archiveClassifier.set("deobf")
//     from sourceSets.main.output
//     manifest {
//         attributes(jar_attributes)
//         attributes 'Maven-Artifact': "${maven_artifact}:deobf"
//     }
// }

artifacts {
    archives shadowJar
    // archives deobfJar
}

// Process resources
processResources {
    inputs.properties mod_properties

    from(sourceSets.main.resources.srcDirs) {
        filesMatching(["**/*.toml", "**/*.mcmeta"]) {
            expand mod_properties
        }
        filesMatching("**/*.json") {
            if (!getPath().contains("/lang/"))
                expand mod_properties
        }
    }
}

// Publishing
publishing {
    publications {
        mod(MavenPublication) {
            // artifact jar
            artifact shadowJar
            artifact sourcesJar
            // artifact javadocJar
            // artifact deobfJar

            pom {
                name = display_name
                url = page
                properties = [
                    description: description
                ]
            }
        }
    }
    repositories {
        maven {
            name "LocalMods"
            url "${project.projectDir.parentFile.toURI()}maven"
        }
    }
    println "${project.projectDir.parentFile.toURI()}maven"
}

// Jar signing
task signJar(type: SignJar, dependsOn: jar) {
    onlyIf {
        project.hasProperty 'keyStore'
    }
    keyStore = project.findProperty('keyStore')
    alias = project.findProperty('keyStoreAlias')
    storePass = project.findProperty('keyStorePass')
    keyPass = project.findProperty('keyStoreKeyPass')
    inputFile = jar.archiveFile
    outputFile = jar.archiveFile
}
build.dependsOn signJar

task loadKeyInfo {
    onlyIf { return file("keyinfo.properties").exists() }

    doFirst {
        def props = new Properties()
        props.load(new FileReader(file("keyinfo.properties")))

        props.each { key, val ->
            project.ext.set(key, val)
        }

        if (!project.hasProperty('keyStore')) throw new GradleException("Key information file 'keyinfo.properties' does not contain a keyStore variable, unable to continue.")
    }
}
signJar.dependsOn loadKeyInfo
